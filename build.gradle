buildscript {
    // version variables
    ext.v_kotlin = '1.2.51'
    ext.v_wala = '1.4.3'
    ext.v_aspectj = '1.9.1'
    ext.v_junit = '5.2.0'
    ext.v_log4j2 = '2.11.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}

group 'ch.uzh.ifi.seal'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = 'ch.uzh.ifi.seal.bencher.MainKt'

defaultTasks 'run'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$v_kotlin"
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile "com.beust:klaxon:3.0.1"
    compile "com.opencsv:opencsv:4.0"
    compile 'org.funktionale:funktionale-all:1.2'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: v_aspectj
    compile group: 'org.aspectj', name: 'aspectjrt', version: v_aspectj
    compile group: 'com.ibm.wala', name: 'com.ibm.wala.core', version: v_wala
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: v_log4j2
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: v_log4j2

    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:$v_junit",
            "org.junit.jupiter:junit-jupiter-params:$v_junit"
    )
    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$v_junit"
    )
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

run {
    def clargs = System.getProperty("args")
    if (clargs != null) {
        args clargs.split(" ")
    }
}

test {
    useJUnitPlatform()
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}
